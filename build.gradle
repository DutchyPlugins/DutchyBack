import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'eclipse'
    id 'maven-publish' 
    id 'signing'         
}

group = 'dev.array21'
version = '2.1.0'

sourceCompatibility = 1.16
targetCompatibility = 1.16

repositories {
	mavenCentral()
    maven{ url "https://hub.spigotmc.org/nexus/content/repositories/snapshots" }
    maven{ url "https://oss.sonatype.org/content/repositories/snapshots" }
	maven { url 'https://jitpack.io' }
    mavenLocal()
}

dependencies {
    compileOnly 'dev.array21:dutchycore:0.1.0'
    compileOnly 'dev.array21:dutchyhome:2.2.0'
    
    compileOnly 'org.spigotmc:spigot-api:1.17-R0.1-SNAPSHOT'
}

task processSource(type: Sync) {
	from sourceSets.main.java
	inputs.property 'version', version
	filter(ReplaceTokens, tokens: [VERSION: version])
	into "$buildDir/src" 
}

compileJava {
	source = processSource.outputs
}

task ghActions(type: Jar) {
	description 'GitHub Actions task. Not used for regular development'
	version = ''
	archiveClassifier = ''
	archiveBaseName = 'output'

	from sourceSets.main.output

	destinationDirectory = file("$rootDir/actions")
}

task testJar(type: Jar) {
	description 'Build a test Jar'
	archiveClassifier = 'DEV'
	from sourceSets.main.output
	destinationDirectory = file("$rootDir/../DutchyCore/server/plugins/DutchyCore/modules/")
}

javadoc.options.addStringOption('Xdoclint:none', '-quiet')

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
	repositories {
		maven {
			name = "ossrh"
			url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
			credentials {
				username = findProperty('ossrhUsername')
				password = findProperty('ossrhPassword')
			}
		}
	}

	publications {
    	mavenJava(MavenPublication) {
      		from components.java
      		artifact sourcesJar
      		artifact javadocJar
      		
			pom {
		    	name = 'DutchyBack'
		    	artifactId = 'dutchback'
		    	packaging = 'jar'
		    	description = 'Minecraft Spigot Back Plugin'
		    	url = 'https://github.com/DutchyPlugins/DutchyBack'
			
				scm {
					connection = 'scm:git:git://github.com/DutchyPlugins/DutchyBack.git'
					developerConnection = 'scm:git:ssh://github.com/DutchyPlugins/DutchyBack.git'
					url = 'https://github.com/DutchyPlugins/DutchyBack'
				}
		
		    	licenses {
		      		license {
		        		name = 'GPL-v3'
		        		url = 'https://github.com/DutchyPlugins/DutchyBack/blob/master/LICENSE'
		      		}
	    		}
		
		    	developers {
		      		developer {
		        		id = 'tobiasdebruijn'
		        		name = 'Tobias de Bruijn'
		        		email = 't.debruijn@array21.dev'
		      		}
		    	}
		    }
    	}
	}
}

signing {
	if(!System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
	    sign publishing.publications.mavenJava
	}}

processResources  {
	duplicatesStrategy = DuplicatesStrategy.INCLUDE
}